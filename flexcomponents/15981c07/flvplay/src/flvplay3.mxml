<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="absolute"
  creationComplete="onStartup()" horizontalScrollPolicy="off" verticalScrollPolicy="off">
<mx:Script>
<![CDATA[
import mx.events.VideoEvent;
import mx.core.UIComponent;
import fl.video.FLVPlayback;

private var player:FLVPlayback = new FLVPlayback();
private var videoList:Array = [];

private function onStartup() : void {
  Security.allowDomain( 'localhost' );
  
  var uic:UIComponent = new UIComponent();
  uic.width = 480;
  uic.height = 360;
  uic.setStyle('top',0);
  uic.setStyle('left',0);
  vidPnl.addChild( uic );
  
  player.addEventListener('skinLoaded',onSkinLoaded);
  player.addEventListener(VideoEvent.COMPLETE,gotoNextMovie);
  uic.addChild( player );

  player.x = 0;
  player.y = 0;
  player.width = 480;
  player.height = 360;
  player.skinBackgroundColor = 0x333333;
  player.skinBackgroundAlpha = 0.6;
  player.skin = "http://localhost/videos/AFComponents_PixelOverlaySkin.swf";
  player.skinAutoHide = false;
  player.autoPlay = true;
  player.scaleMode = "maintainAspectRatio";
}
private function onSkinLoaded( event:Object ) : void {
  videos.send();
}
private function onVideoListLoad() : void { 
  for each( var vid:XML in videos.lastResult..video )
    videoList.push( 'http://localhost/videos/'+vid.@src );
  gotoNextMovie();
}
private function gotoNextMovie( event:Event = null ) : void {
  if ( videoList.length > 0 )
    player.source = videoList.pop();
}
]]>
</mx:Script>
<mx:HTTPService id="videos" url="http://localhost/videos/videos.xml" resultFormat="e4x"
	result="onVideoListLoad()" />
<mx:Image source="@Embed('flower_bottom.png')" />
<mx:Panel id="vidPnl" title="Video Player" paddingBottom="5" paddingLeft="5"
  paddingRight="5" paddingTop="5" top="50" left="50" />
<mx:Image source="@Embed('flower_top.png')" mouseEnabled="false" />
</mx:Application>
