<?xml version="1.0" encoding="utf-8"?>
<mx:Application xmlns:mx="http://www.adobe.com/2006/mxml" layout="vertical"
  xmlns:vg="org.un.cava.birdeye.ravis.graphLayout.visual.*" creationComplete="bands.send()">
<mx:HTTPService id="bands" url="http://localhost/music/music.xml" resultFormat="e4x" result="onBands(event);" />
<mx:Script>
<![CDATA[
import org.un.cava.birdeye.ravis.graphLayout.visual.edgeRenderers.BaseEdgeRenderer;
import org.un.cava.birdeye.ravis.graphLayout.layout.ConcentricRadialLayouter;
import org.un.cava.birdeye.ravis.graphLayout.data.Graph;
import org.un.cava.birdeye.ravis.graphLayout.visual.IVisualNode;
import mx.rpc.events.ResultEvent;

private function addMembers( parent:IVisualNode, band:XML ) : void {
  for each( var member:XML in band.member ) {
    var memberNode:IVisualNode = vgraph.createNode(member.toString());
    memberNode.data = { name: member.toString(), color: 0x0000FF, image:null };
    vgraph.linkNodes( parent, memberNode );
  }
}
private function addBands( parent:IVisualNode, genre:XML ) : void {
  for each( var band:XML in genre.band ) {
    var bandNode:IVisualNode = vgraph.createNode(band.@name.toString());
    bandNode.data = { name: band.@name.toString(), color: 0x00FF00,
      image: 'http://localhost/music/'+band.@image.toString() };
    vgraph.linkNodes( parent, bandNode );
    addMembers( bandNode, band );
  }
}
private function onBands( event:ResultEvent ) : void {
  vgraph.graph = new org.un.cava.birdeye.ravis.graphLayout.data.Graph("genres");

  var rootNode:IVisualNode = vgraph.createNode("root");
  rootNode.data = { name: 'Genres', color: 0x333333, image:null };
  for each ( var genre:XML in event.result..genre ) {
    var genreNode:IVisualNode = vgraph.createNode(genre.@name.toString());
    genreNode.data = { name: genre.@name.toString(), color: 0xFF0000 };
    vgraph.linkNodes( rootNode, genreNode );
    addBands( genreNode, genre );
  }

  vgraph.layouter = new ConcentricRadialLayouter(vgraph);
  vgraph.layouter.autoFitEnabled = true;
  vgraph.edgeRenderer = new BaseEdgeRenderer(vgraph.edgeDrawGraphics);
  vgraph.maxVisibleDistance = 2;
  vgraph.displayEdgeLabels = false;
  vgraph.currentRootVNode = rootNode;
  vgraph.draw();
}
]]>
</mx:Script>
<mx:Panel width="100%" height="100%" title="Bands">
<vg:VisualGraph id="vgraph" width="100%" height="100%"
  itemRenderer="MyNodeRenderer" />
</mx:Panel>
</mx:Application>
